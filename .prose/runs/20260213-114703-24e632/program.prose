# land.prose
# End-of-session cleanup for ypi.
#
# Run: rp ypi .prose/land.prose
# Or just follow the steps manually â€” this is a checklist, not automation.
#
# All deterministic steps use exec (no subagent needed).
# Sessions only for judgment calls (describe, reflect).

# --- Gate 1: Fast tests ---

let fast = exec "cd /home/raw/Documents/GitHub/ypi && make test-fast 2>&1 | tail -15"

gate **all fast tests pass (72/72 or more)**:
  evidence: fast
  on-reject: throw "Fix failing fast tests before landing."

# --- Gate 2: E2E tests ---

let e2e = exec "cd /home/raw/Documents/GitHub/ypi && make test-e2e 2>&1 | tail -25"

gate **all e2e tests pass**:
  evidence: e2e
  on-reject: throw "Fix failing e2e tests before landing."

# --- Gate 3: Smoke test ---

let smoke = exec "cd /home/raw/Documents/GitHub/ypi && echo '2+2=' | rlm_query 'What is the answer? Just the number.' 2>/dev/null"

gate **smoke test returns 4**:
  evidence: smoke
  on-reject: throw "rlm_query is broken. Fix before landing."

# --- Describe the change (needs judgment) ---

let status = exec "cd /home/raw/Documents/GitHub/ypi && jj diff --stat 2>&1"
let log = exec "cd /home/raw/Documents/GitHub/ypi && jj log --limit 3 2>&1"

session "Describe this change"
  prompt: """
    Look at the jj diff --stat and jj log below. Write a concise commit message
    and run: jj describe -m "your message"

    The message should summarize what shipped. Use conventional commit style
    (feat:, fix:, refactor:, etc.). One line, under 80 chars if possible.

    jj diff --stat:
    {status}

    jj log:
    {log}
  """
  context: [status, log]

# --- Deterministic landing steps ---

let encrypt = exec "cd /home/raw/Documents/GitHub/ypi && bash scripts/encrypt-prose 2>&1 | tail -5"

let push = exec """
cd /home/raw/Documents/GitHub/ypi
jj bookmark set master 2>&1
jj git push 2>&1
"""

gate **push succeeded (no errors in output)**:
  evidence: push
  on-reject: throw "Push failed. Fix and retry."

let decrypt = exec "cd /home/raw/Documents/GitHub/ypi && bash scripts/decrypt-prose 2>&1 | tail -5"

# --- Clean up stale workspaces ---

let workspaces = exec "cd /home/raw/Documents/GitHub/ypi && jj workspace list 2>&1"
let cleanup = exec """
cd /home/raw/Documents/GitHub/ypi
for ws in $(jj workspace list 2>/dev/null | grep 'rlm-d' | awk '{print $1}'); do
  jj workspace forget "$ws" 2>&1
done
echo "Cleanup done"
"""

# --- Verify ---

let verify = exec "cd /home/raw/Documents/GitHub/ypi && jj log --limit 3 2>&1 && echo '---' && jj status 2>&1"

gate **master is pushed and working copy is clean**:
  evidence: verify
  on-reject: throw "Landing incomplete. Check jj log and jj status."

# --- Reflect (needs judgment) ---

output summary = session "Reflect and hand off"
  prompt: """
    The landing is complete. Reflect on this session and write a handoff.

    Consider:
    1. What shipped? (check jj log)
    2. What did we learn? Any surprises or failed experiments?
    3. Did we encode the learning? (tests > AGENTS.md > SYSTEM_PROMPT.md > README.md)
    4. What should the next agent try?
    5. Current version (check package.json)
    6. Any gotchas or context for the next session?

    The goal is compounding: each session leaves ypi slightly better at building itself.
  """
  context: verify
